//created on: 2015-12-2
package com.drools.MeeHealth

import com.drools.MeeHealth.model.FlightStatus
import com.drools.MeeHealth.model.FlightControl
import com.drools.MeeHealth.model.EmergencySignal
import com.drools.MeeHealth.model.EmergencyInCourse

//list any import classes here.

global com.drools.MeeHealth.model.FlightControl control;
global com.drools.MeeHealth.model.EmergencySystem emergencySystem;


declare FlightStatus
    @role(event)
end

declare EmergencySignal
    @role(event)
end

//declare any global variables here

rule "First contact"
salience 100
	when
		$currentFlight : FlightStatus()  from entry-point "flight-control"
		not (exists (FlightStatus(this != $currentFlight, flight == $currentFlight.flight) from entry-point "flight-control"))
	then
		control.addFlight($currentFlight);
		System.out.println("First contact with Flight " + $currentFlight.getFlight());
end

rule "flight arrival"
when
    $flight : FlightStatus() from entry-point "flight-arrival"
    // Obtain resources to prepare the flight landing
then
    System.out.println("Flight " + $flight.getFlight() + " arriving to " + control.getAirport() + ". Sending instructions");
    // Send instructions to arriving flight
end

rule "emergency system"
	when
		$emergency : EmergencySignal() from entry-point "emergency-channel"
		FlightStatus(flight==$emergency.flight,
		this before[0m, 10m] $emergency)
		from entry-point "flight-arrival"
	then
		// Emergency in progress
		System.out.println("Emergency in progress. Flight " +
		$emergency.getFlight());
	insert (new EmergencyInCourse($emergency.getFlight()));
end

rule "flight average speed"
when
    $flight : FlightStatus() from entry-point "flight-control"
    $averageSpeed : Number(floatValue > 0) from accumulate(FlightStatus(flight==$flight.flight, $speed:speed) over window:length(5) 
                                                          from entry-point "flight-control",
                                                          average($speed))
then
    System.out.println($flight.getFlight() + " average speed: " + $averageSpeed);
end

rule "landing system"
when
    $flight : FlightStatus() from entry-point "flight-landing"
    FlightStatus(this!=$flight, this before[0m, 3m] $flight) from entry-point "flight-landing"
then
    emergencySystem.redirect($flight);
end
